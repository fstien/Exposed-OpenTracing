apply plugin: 'maven-publish'
apply plugin: 'signing'

task libSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives libSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                artifact("$buildDir/libs/lib-${version}.jar")

                artifact libSourcesJar
                artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'OpenTracing instrumentation of Exposed (Kotlin ORM Framework).'
                    url = 'https://github.com/fstien/Exposed-OpenTracing'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/fstien/Exposed-OpenTracing/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'fstien'
                            name = 'Francois Stiennon'
                            email = 'francois.stiennon@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/fstien/Exposed-OpenTracing.git'
                        developerConnection = 'scm:git:ssh://github.com/fstien/Exposed-OpenTracing.git'
                        url = 'https://github.com/fstien/Exposed-OpenTracing'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
